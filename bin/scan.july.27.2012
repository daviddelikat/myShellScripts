#!/usr/bin/perl
use warnings;
use strict;

=pod DOING


=cut

$| = 1;

sub RUNTIME { 10 }

binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';
use open qw(:std :utf8);

use Getopt::Long;
use File::chdir;
use MongoDB;
use Data::GUID;
use File::Spec::Functions ':ALL' ;
use JSON;
use IO::All;
use Hash::Diff 'diff' ;
use List::MoreUtils 'zip';
use File::Basename;
use Sys::Hostname ();
use File::Find::Rule;
use Convert::AnyBase;

{
my $base32 = Convert::AnyBase->crockford;
my $hex = Convert::AnyBase->hex;
sub hex2b32 { $base32->encode($hex->decode(shift)) }
}

my $HOSTNAME = Sys::Hostname->hostname;

#{ # dir stack
#my @dirstack;
#sub pushdir { push @dirstack, `pwd`; chdir shift }
#sub cwd { `pwd` }
#sub popdir { chdir pop @dirstack }
#}

sub HDR { 't00json' }

sub newId { Data::GUID->new->as_base64 }
sub idAsHex { Data::GUID->from_any_string(shift)->as_hex }
sub idAsB64 { Data::GUID->from_any_string(shift)->as_base64 }
sub idAsB32 { hex2b32(Data::GUID->from_any_string(shift)->as_base64) }

sub DEVMODE { 1 }

{
    my $db;
    sub db { 
	$db ||= do {
	    if( DEVMODE ) {
		MongoDB::Connection->new->devscan;
	    } else {
		MongoDB::Connection->new->scan;
	    }
	}
    }
}

my $contentFileName = '.contents';

sub loadDataFile {
    my $dataFile = $contentFileName;
    my $io = io $dataFile;
    return { } unless -s $dataFile;
    chomp( my $hdr = $io->getline );
    die 'unknown header ',$hdr unless $hdr eq HDR;
    my $dataText = $io->slurp;
    my $data = eval { decode_json( $dataText ) };
    die "failed to read '$CWD':$@" if $@;
    return $data;
}

sub saveDataFile {
    my($data) = @_;
    my $dataFile = $contentFileName;
    my $io = io $dataFile;
    print 'save data file ',`pwd`;
    #use Data::Dumper; print Dumper $data;
    #$io->println( HDR,"\n",JSON->new->pretty->encode($data) );
    $io->println( HDR,"\n",encode_json($data) );
}

sub loadEntry {
    my($dir,$name) = @_;
    local $CWD = $dir;
    my $contents = loadDataFile;
    return $contents->{$name} || { };
}

sub saveEntry {
    my($dir,$name,$entry) = @_;
    local $CWD = $dir;
    my $contents = loadDataFile;
    $contents->{$name} = $entry;
    saveDataFile($contents);
}

my @STnames = qw(device inode mode nlink uid gid rdev size
	       atime mtime ctime blksize blocks);
sub getStats {
    my $file = shift;
    my @stats = stat($file);
    return { } unless @stats;
    return { zip @STnames, @stats }
}

sub getDigest {
    my $name = shift;
    return if -d $name;
    my $shaFilePath = '.shalist';
    return unless $shaFilePath && -f $shaFilePath ;
    warn "$shaFilePath\n";
    chomp( my @shas = io($shaFilePath)->slurp );
    warn @shas;
    return unless @shas;
    for my $line ( @shas ) {
	#print $line,"\n";
        next unless $line =~ /^(.+)  (.*)$/;
	#print $2,"\n";
	return $1 if $2 eq $name;
    }
    # TODO create a remote request for the sha1 digest of the file
    return;
}

sub updateStats {
    my $entry = shift;
    my $stats = getStats($entry->{path});
    $stats->{type} = delete $entry->{type};
    my $digest = getDigest($entry->{name});
    $stats->{digest} = $digest if $digest;
    $stats->{host} = $HOSTNAME;
    $stats->{unam} = getpwuid($stats->{uid});
    $stats->{gnam} = getgrgid($stats->{uid});
    if( $entry->{stats} ) {
        my $res = diff($stats, $entry->{stats});
	delete $res->{mtime};  # don't need to update for mtime
	return unless %$res;
    }
    $entry->{stats} = $stats;
    my $event = {
        date => '' . localtime,
	type => 'entry changed',
	_id => newId,
	entry_id => $entry->{_id},
	stats => $stats,
    };
    db->events->insert($event);
    return;
}

sub fetchDB {
    my $entry = shift;
    my $query;
    $query->{_id} = $entry->{id} if $entry->{id};
    unless( %$query ) {
	$query->{path} = $entry->{path} if $entry->{path};
    }
    my $new = db->find_one($query);
    %$entry = %$new if $new;
}

sub updateDB {
    my $entry = shift;
    $entry->{_id} ||= $entry->{id};
    db->entries->save($entry);
}

sub removeEntry {
    my($data,$name) = @_;
    return unless exists $data->{entries}{$name};
    deleteDB(delete $data->{entries}{$name});
}

sub deleteDB {
    my $entry = shift;
    db->entries->remove({_id => $entry->{_id}});
}

sub updatePath {
    my $path;
    $path = catfile(@_) if @_ > 1;
    $path ||= shift;
    File::Spec->rel2abs($path);
}

sub defaultEntry {
    my $entry = shift;
    print $entry->{path},"\n";
    $entry->{path} = updatePath($entry->{path});
    $entry->{name} = basename $entry->{path};
    $entry->{parentId} ||= 'unknown';
    $entry->{task} ||= 'validate';
    $entry->{state} ||= 'new';
    $entry->{status} ||= 'new';
    $entry->{class} ||= 'fileSysEntry',
    $entry->{id} ||= newId;
}

sub  array2RE {
    my $s = join '|', map { '^'.$_.'$' } @_;
    return qr/$s/;
}

my $ignoreRE = array2RE( qw{
    \.Trash
    lost\+found
    \.contents
    \..*\.swp
} );

my $deleteRE = array2RE( qw{
    \.DS_Store
} );

sub processEntry {
    my $parms = @_ == 1 ? $_[0] : do {
	local $" = ' ';
	die "bad parms:@_" if @_ %2;
	+{ @_ };
    };
    local $, = ' ';
    print "processing entry: ",%$parms,"\n";
    my $entry;
    $entry = db->entries->find_one( { _id => $parms->{id} } ) if $parms->{id};
    $entry ||= do {
	if( defined $parms->{path} ) {
	    $parms->{path} = updatePath($parms->{path});
	} else { local $" = ' ';
	    die "both name and path are missing:%$parms" unless defined $parms->{name};
	    $parms->{path} = updatePath($CWD,$parms->{name});
	}
	db->entries->find_one( { path => $parms->{path} } );
    };
    $entry = $entry ? { %$parms, %$entry } : { %$parms };
    my $item = io $entry->{path};
    $entry->{type} =
		     $item->is_file ? 'file'      :
		     $item->is_dir  ? 'directory' :
		     $item->is_link ? 'link'      :
		     'other';
    $entry->{parentId} ||= do {
	delete $entry->{parentPath};
	my $x = db->entries->find_one( { path => $parms->{parentPath} } );
	$x->{id} if $x;
    } if $parms->{parentPath};
    defaultEntry($entry);
    updateStats($entry);
    updateDB($entry);
    return $entry;
}

sub processDir {
    my $dirpath = shift;
    return unless -d $dirpath;
    print "processing dir:$dirpath\n";
    my (undef,$parent,$dirname) = splitpath($dirpath);
    local $CWD = $dirpath;
    my $data = eval { loadDataFile };
    do{warn $@;return} if $@;
    $data ||= { path => $dirpath };
    my $entries = $data->{entries} ||= { };
    $entries->{'..'} ||= processEntry( path => $parent );
    $entries->{'.'} ||= processEntry( path => $dirpath,
               parentId => $entries->{'..'}{id} );
    my $dir = io $CWD;
    for my $item ( $dir->all ) {
	my $name = basename $item->name;
	print "loop: $name\n";
	if( $name =~ $ignoreRE ) {
	    removeEntry($data,$name);
	    next;
	} elsif( $name =~ $deleteRE ) {
	    removeEntry($data,$name);
	    unlink $name;
	    next;
	}
	$entries->{$name} = processEntry( name => $name, path => $item->name );
        if( $item->is_dir ) {
	    local $CWD = $item->name;
	    saveDataFile( {
		    entries => { '.' => $entries->{$name},
				 '..'=> $entries->{'.'} },
		    path => $CWD,
		} );
	}
    }
    saveDataFile($data);
}

sub processEntries {
    my $set = db->todo->find;
    my $endtime = time + RUNTIME;
    while( my $item = $set->next ) {
        eval { processDir( $item->{path} ) };
	warn $@ if $@;
	db->todo->remove({ path => $item->{path} });
	last if time > $endtime;
    }
}

sub findItems {
    my $endtime = time + RUNTIME;
    db->wip->insert( { path => updatePath($_) } ) for @_;
    while( my $item = db->wip->find_one() ) {
	my $path = $item->{path};
	db->wip->remove( { path => $path } );
	next unless -d $path;
	next if basename($path) =~ $ignoreRE ;
	next if db->todo->count( { path => $path } );
	db->todo->insert( { path => $path } );
	$_->is_dir && db->wip->insert( { path => '' . $_ } ) for io($path)->all;
	last if time > $endtime;
    }
}

sub setMountPoint {
    # identify mounted file systems
    # add enries for each
    # container entries go in root dir of mounted fs and in database
    # process entries; set 'container' = id of mount container
}

sub addDigest {
    # if container is set
    # if container is local ( not a network mount )
    # calculate digest for files which have none assigned
    # find or create a digest document
}

sub linkMatches {
    # if entries are in same container
    # if file size & digest match
    # create hard link for one file
}

sub listEntries {
   my $set = db->entries->find;
   while( my $i = $set->next ) {
       print $i->{id},"\t",$i->{path}, "\n";
   }
}

sub listAll {
   my $set = db->entries->find;
   while( my $i = $set->next ) {
       print JSON->new->pretty->encode($i),"\n";
   }
}

sub listX {
   my $set = shift->find;
   while( my $i = $set->next ) {
       print $i->{path}, "\n";
   }
}

sub listWIP {
    listX( db->wip );
}

sub listTODO {
    listX( db->todo );
}

sub yesno {
    my($flag,$msg) = @_;
    my($all,$strict,$default) = (0,0,'no');
    if( $flag eq 'strict' ) { $strict = 1;
    } elsif( $flag eq 'all' ) { $all = $strict = 1;
    } elsif( $flag =~ /^(yes|no)$/i ) { $default = lc $flag;
    } else { $msg ||= $flag;
    }
    if( $all ) { $msg .= ' (YES or NO) > ';
    } elsif( $strict ) { $msg .= ' (Yes or No) > ';
    } elsif( $default eq 'yes' ) { $msg .= ' ([Yes] or No) > ';
    } else { $msg .= ' (Yes or [No]) > ';
    }
    my $res;
    until(defined $res) {
        print $msg;
	chomp(my $line = lc <>);
	next if $strict && $line eq '';
	$line =~ s/^\s+//;
	unless( $all ) {
	    $res = ($line||$default) =~ /^y/;
	} elsif( $line =~ /^(yes|no)$/ ) {
	    $res = ($line eq 'yes')
	}
    }
    print "\nyes\n" if $res;
    return $res;
}

sub cleanAll {
    return unless DEVMODE;
    return unless 'y' eq ($_[0]||'n')
           or yesno( 'do you really want to erase everything?' );
    my $set = db->entries->find({'stats.type' => 'directory'});
    while( my $item = $set->next ) {
	my $path = updatePath( $item->{path}, $contentFileName );
	next unless -f $path;
	print '.';
	#print 'unlink ',$path,"\n" ; # if -f $path;
	unlink $path;
    }
    db->drop;
    print "!\n";
}

sub listContents {
    my $set = File::Find::Rule->file->name($contentFileName)->start('.');
    while( my $item = $set->match ) {
        print $item,"\n";
	local $CWD = dirname rel2abs $item;
	my $data = loadDataFile;
	for my $entry ( values %{$data->{entries}} ) {
	    print ' > ', $entry->{id},' ',$entry->{path}, "\n";
	}
    }
}

my @DFnames = qw/
   filesys total used avail cap mount
/;
my $DFblockSize = 4096;
sub insertDrive {
    my @l = split /\s+/, shift;
    return if $l[0] eq 'Filesystem';
    my %l = zip @DFnames, @l;
    $l{total} *= $DFblockSize;
    $l{avail} *= $DFblockSize;
    $l{used} *= $DFblockSize;
    my $drivePath = $l{mount};
    my $deviceNum = getStats($drivePath)->{device};
    my $drive = db->drives->find_one({
        'hosts.' . $HOSTNAME . '.deviceNum' => $deviceNum,
    }) || +{ } ;
    my $filename = catfile($drivePath,$contentFileName);
    print "cannot write $filename\n" unless -w $drivePath || -w $filename;
    local $CWD = $drivePath;
    my $data = loadDataFile;
    if( $data && $data->{drive} ) {
        $drive = { %$drive, %{$data->{drive}} };
    } else {
        $data->{drive} = $drive;
    }
    my $createEvent;
    $drive->{_id} ||= 
    $drive->{id} ||= newId;
    if( ($drive->{total}||-1) != $l{total}
     || ($drive->{avail}||-1) != $l{avail}
     || ($drive->{used}||-1)  != $l{used} ) {
	$createEvent = 1;
	$drive->{total} = $l{total};
	$drive->{avail} = $l{avail};
	$drive->{used} = $l{used};
	$drive->{capacity} = $l{cap};
    }
    if( my $hostEntry = $drive->{hosts}{$HOSTNAME} ) {
        if( $hostEntry->{deviceNum} != $deviceNum
	 || $hostEntry->{drivePath} ne $drivePath ) {
	    $createEvent = 1;
	    $hostEntry->{deviceNum} = $deviceNum;
	    $hostEntry->{drivePath} = $drivePath;
	}
    } else {
        $createEvent = 1;
	$drive->{hosts}{$HOSTNAME} = {
	    deviceNum => $deviceNum,
	    drivePath => $drivePath,
	};
    }
    db->drives->insert($drive);
    saveDataFile($data);
    if( $createEvent ) {
        db->events->insert( {
	    _id => newId,
	    date => '' . localtime,
	    type => 'drive mounted/changed',
	    entry_id => $drive->{id},
	    host => $HOSTNAME,
	    drivePath => $drivePath,
	    deviceNum => $deviceNum,
	    currentSpace => {
		total => $drive->{total},
		avail => $drive->{avail},
		used => $drive->{used},
		capacity => $drive->{capacity},
	    },
	} );
    }
}

sub listDev {
    my $set = db->drives->find;
    while( my $i = $set->next ) {
        print $i->{_id},' ',$i->{hosts}{$HOSTNAME}{drivePath},"\n";
    }
}

sub findDrives {
    my $h = io "df -PB $DFblockSize|";
    my @l = $h->slurp;
    $_ && insertDrive($_) for @l;
}

sub runTest {
    return unless DEVMODE;
    return unless yesno( 'run test deletes data, ready?' );
    print '-'x10 . "cleanAll\n";cleanAll('y');
    print '-'x10 . "findItems\n";findItems('.');
    print '-'x10 . "listWIP\n";listWIP;
    print '-'x10 . "listTODO\n";listTODO;
    print '-'x10 . "listEntries\n";listEntries;
    print '-'x10 . "processEntries\n";processEntries;
    print '-'x10 . "listEntries\n";listEntries;
    print '-'x10 . "listContents\n";listContents;
    print '-'x10 . "done\n";
}

GetOptions(
    0 => sub { findDrives; exit; },
    1 => sub { findItems(@ARGV); exit },
    2 => sub { processEntries; exit },
    4 => sub { addDigest; exit },
    5 => sub { linkMatches; exit },
    ls => sub { listEntries; exit },
    la => sub { listAll; exit },
    ld => sub { listDev; exit },
    wip => sub { listWIP; exit },
    todo => sub { listTODO; exit },
    clean => sub { cleanAll; exit },
    run => sub { runTest; exit },
);

print "select one of step 1 through 5\n";

